" load plugins
call pathogen#infect()

"""""""""""""""""""""""""""""""""""""""""""
" BASIC CONFIGURATION
"""""""""""""""""""""""""""""""""""""""""""
set nocompatible

" Set encoding
set encoding=utf-8

" allow hidden dirty buffers
set hidden

" line length
set formatoptions=qrn1
set textwidth=79
set colorcolumn=80

" Whitespace stuff
set nowrap
set tabstop=2
set shiftwidth=2
set softtabstop=2
set expandtab

" list and invisible characters
set list
set listchars=tab:▸\ ,trail:·,eol:¬

" Searching
set hlsearch
set incsearch
set ignorecase
set smartcase

" allow backspacing over everything in insert mode
set backspace=indent,eol,start

" Use modeline overrides
set modeline
set modelines=10

" Directories for swp files
set backup
set backupdir=~/.vim/backup
set directory=~/.vim/backup
set undodir=~/.vim/undo

" Prevent Vim from clobbering the scrollback buffer. See
" http://www.shallowsky.com/linux/noaltscreen.html
set t_ti= t_te=

" Tab completion
set wildmode=list:longest,list:full
set wildmenu

" use system clipboard for yanking
set clipboard=unnamed

" i hate folding.
set nofoldenable

" setup the leader key
" let mapleader = ","

"""""""""""""""""""""""""""""""""""""""""""
" CUSTOM AUTOCMDS
"""""""""""""""""""""""""""""""""""""""""""

" Remember last location in file
if has("autocmd")
  " Clear all autocmds in the group
  autocmd!

  " Jump to last cursor position unless it's invalid or in an event handler
  au BufReadPost * if line("'\"") > 0 && line("'\"") <= line("$")
    \| exe "normal g'\"" | endif

  " source .vimrc upon changes
  autocmd bufwritepost .vimrc source $MYVIMRC
endif

"""""""""""""""""""""""""""""""""""""""""""
" LOOKS
"""""""""""""""""""""""""""""""""""""""""""

syntax on

set relativenumber
set ruler

" Status bar
set laststatus=2

set t_Co=256
set cursorline

" highlight current line
set cul

if has("gui_running")
  source ~/.gvimrc
else
  " set background=light
  " call togglebg#map("<F5>")
  color Tomorrow-Night
endif

" Show (partial) command in the status line
set showcmd

" keep at least 3 lines around the cursor
set scrolloff=3

"""""""""""""""""""""""""""""""""""""""""""
" MAPPINGS & SHORTCUTS
"""""""""""""""""""""""""""""""""""""""""""

" clear search
map <leader><space> :noh<cr>

" use ack
nnoremap <leader>a :Ack

" makes ,w split windows vertically
nnoremap <leader>w <C-w>v<C-w>l

" reselect pasted text
nnoremap <leader>v V`]

" open vimrc
nmap <leader>v :tabedit $MYVIMRC<CR>

" reformat current paragraph
map <leader>l gqip

" remove trailing whitespace
nnoremap <silent> <F6> :let _s=@/<Bar>:%s/\s\+$//e<Bar>:let @/=_s<Bar>:nohl<CR>

" spell checking
nmap <silent> <leader>s :set spell!<CR>
" Set region to American English
set spelllang=en_us

"Works in normal mode, must press Esc first"
:map <M-s> :w<kEnter>
"Works in insert mode, saves and puts back in insert mode"
:imap <M-s> <Esc>:w<kEnter>i

nmap <leader>0 %!python -m json.tool<CR>

nmap <leader>o :set paste!<CR>

nmap <C-n> :bnext<CR>
nmap <C-p> :bprev<CR>

"""""""""""""""""""""""""""""""""""""""""""
" MOVES
"""""""""""""""""""""""""""""""""""""""""""

" split window navigation
nnoremap <C-h> <C-w>h
nnoremap <C-j> <C-w>j
nnoremap <C-k> <C-w>k
nnoremap <C-l> <C-w>l

" use hjkl for movement
nnoremap <up> <nop>
nnoremap <down> <nop>
nnoremap <left> <nop>
nnoremap <right> <nop>

" I don't want mouse scrolling.
map <ScrollWheelUp> <nop>
map <S-ScrollWheelUp> <nop>
map <C-ScrollWheelUp> <nop>
map <ScrollWheelDown> <nop>
map <S-ScrollWheelDown> <nop>
map <C-ScrollWheelDown> <nop>
map <ScrollWheelLeft> <nop>
map <S-ScrollWheelLeft> <nop>
map <C-ScrollWheelLeft> <nop>
map <ScrollWheelRight> <nop>
map <S-ScrollWheelRight> <nop>
map <C-ScrollWheelRight> <nop>

"""""""""""""""""""""""""""""""""""""""""""
" FILETYPE SETUP
"""""""""""""""""""""""""""""""""""""""""""

filetype plugin on

" load the plugin and indent settings for the detected filetype
filetype plugin indent on

" make uses real tabs
au FileType make set noexpandtab

" Enable syntastic syntax checking
let g:syntastic_enable_signs=1
let g:syntastic_quiet_warnings=1

" Thorfile, Rakefile, Vagrantfile and Gemfile are Ruby
au BufRead,BufNewFile {Gemfile,Rakefile,Vagrantfile,Thorfile,config.ru} set ft=ruby

" Ruby
let g:rubycomplete_classes_in_global = 1
let g:rubycomplete_rails = 1

" add json syntax highlighting
au BufNewFile,BufRead *.json set ft=javascript

" make Python follow PEP8 ( http://www.python.org/dev/peps/pep-0008/ )
au FileType python set softtabstop=4 tabstop=4 shiftwidth=4 textwidth=79

" C settings
au FileType c set softtabstop=4 tabstop=4 shiftwidth=4 textwidth=79

" coffeescript styles
au BufNewFile,BufReadPost *.coffee setl shiftwidth=2 expandtab

" hamlc syntax highlighting
au BufRead,BufNewFile *.hamlc set ft=haml

"""""""""""""""""""""""""""""""""""""""""""
" PLUGIN CONFIGURATION
"""""""""""""""""""""""""""""""""""""""""""
" use https://github.com/ggreer/the_silver_searcher instead of ack
let g:ackprg = 'ag --nogroup --nocolor --column'

set wildignore+=*.o,*.obj,.git,*.rbc,*.class,.svn,vendor/gems/*,*.jpg,*.png
set wildignore+=*.gif,*.toc,*.aux,*.bbl,*.dvi,*.pdf,*.lof,*.blg,*.out,*.log
set wildignore+=*.gitkeep,*.jpeg,tags,*.dat,coverage/*

" set open command for ruby_doc
let g:ruby_doc_command='open'

" syntastic settings
let g:syntastic_mode_map = { 'mode': 'active',
                           \ 'active_filetypes': ['ruby', 'coffee', 'sass',
                           \ 'eruby', 'haml', 'javascript', 'html'],
                           \ 'passive_filetypes': ['puppet'] }

" ctrl+p ignore settings
set wildignore+=*/tmp/*,*.so,*.swp,*.zip,*.sass-cache/*,*.DS_Store
set wildignore+=tmp\*,*.swp,*.zip,*.exe,*.gem

let g:ctrlp_custom_ignore = {
  \ 'dir':  '\.git$\|\.hg$\|\.svn$',
  \ 'file': '\.exe$\|\.so$\|\.dll$',
  \ 'link': 'some_bad_symbolic_links',
  \ }

"""""""""""""""""""""""""""""""""""""""""""
" FUNCTIONS
"""""""""""""""""""""""""""""""""""""""""""

"""""""""""""
" RENAME CURRENT FILE
"""""""""""""
function! RenameFile()
    let old_name = expand('%')
    let new_name = input('New file name: ', expand('%'), 'file')
    if new_name != '' && new_name != old_name
        exec ':saveas ' . new_name
        exec ':silent !rm ' . old_name
        redraw!
    endif
endfunction
map <leader>n :call RenameFile()<cr>

"""""""""""""
" RELATIVE/NON RELATIVE LINE NUMBERS
"""""""""""""
"relative number switching
function! g:ToggleNuMode()
  if(&rnu == 1)
    set nu
  else
    set rnu
  endif
endfunction
nnoremap <C-a> :call g:ToggleNuMode()<cr>

"""""""""""""
" TEST RUNNER
"""""""""""""
map <leader><CR> :call RunTest()<cr>

function! RunTest()
  :wa
  let filename = @%

  if filename =~ '_test.rb'
    call RunTestUnit(filename)
  elseif filename =~ '_spec.rb'
    call RunRSpec(filename)
  elseif filename =~ '.feature'
    call RunCucumber(filename)
  else
    call FindCorrespondingTest(filename)
  endif
endfunction

function! RunRSpec(filename)
  if filereadable("Gemfile")
    exec ":!clear && bundle exec rspec -c " . a:filename
  else
    exec ":!clear && rspec -c " . a:filename
  end
endfunction

function! RunTestUnit(filename)
  exec ":!clear && bundle exec ruby -Ilib:bin:test:. -rtest/unit " . a:filename
endfunction

function! RunCucumber(filename)
  if filereadable("Gemfile")
    exec ":!clear && bundle exec cucumber " . a:filename
  else
    exec ":!clear && cucumber " . a:filename
  end
endfunction

function! FindCorrespondingTest(filename)
  " file itself is not a test file
  " try to find a corresponding test file
  " XXX: this is pretty ugly right now but it works..
  "
  " BUG: produces for some files the following error:
  "         Error detected while processing function
  "         RunTest..FindCorrespondingTest..RunRSpec:
  "         E486: Pattern not found: spec
  "     although the spec runs as expected

  " from app/controller/ticket.rb to ticket.rb
  let filename = substitute(a:filename, '\w\+\/\w\+\/', '', 'g')

  " find RSpec
  let test_file = substitute(filename, '\.rb', '_spec.rb', 'g')
  let result = system("find . | grep '" . test_file . "'")
  if result =~ 'spec'
    let result = substitute(result, '\n', '', g)
    call RunRSpec(result)
  else
    " find Test::Unit
    let test_file = substitute(filename, '\.rb', '_test.rb', 'g')
    let result = system("find . | grep '" . test_file . "'")
    if result =~ 'test'
      let result = substitute(result, '\n', '', g)
      call RunTestUnit(result)
    endif
  endif
endfunction
